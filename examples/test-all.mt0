<% "Running all examples" :: newl

start = now()
args = get_launch_args()

this = File@filename(args[0])
root = new: File((new: File(args[0])).parent())

if: (!root.exists() || !root.is_dir())
	die($"Failed to open directory {}", root)

title = func: char, str => "", width => 50 {
	pad = char.repeat((width - str.size()) / 2 - 2)
	space = str.is_empty() ? "" : " "
	title = pad :: space :: str :: space :: pad
	if: ((n = width - title.size()) > 0)
		title[] = char.repeat(n)

	return: title
}

tmp = new: File("")
count = 0
root.for_each(f: file {
	tmp.set(file)
	if: (tmp.extension() != ".mt0" || tmp.filename() == #this)
		nil!

	<% newl :: title("v", file)
	try { 
		time = measure_time(load(file))
	}
	catch: ex {
		<% $"{} threw an exception: ", ex
	}
	<% title("^", "Finished in {} secs".format(time / 1000.0)) :: newl
	++count
})
<% $"Done running examples ({} in total). {} secs elapsed." count, (now() - start) / 1000.0